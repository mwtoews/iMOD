!!  Copyright (C) Stichting Deltares, 2005-2018.
!!
!!  This file is part of iMOD.
!!
!!  This program is free software: you can redistribute it and/or modify
!!  it under the terms of the GNU General Public License as published by
!!  the Free Software Foundation, either version 3 of the License, or
!!  (at your option) any later version.
!!
!!  This program is distributed in the hope that it will be useful,
!!  but WITHOUT ANY WARRANTY; without even the implied warranty of
!!  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
!!  GNU General Public License for more details.
!!
!!  You should have received a copy of the GNU General Public License
!!  along with this program.  If not, see <http://www.gnu.org/licenses/>.
!!
!!  Contact: imod.support@deltares.nl
!!  Stichting Deltares
!!  P.O. Box 177
!!  2600 MH Delft, The Netherlands.
!!
MODULE MOD_SPOINTS_UTL

USE WINTERACTER
USE RESOURCE
USE IMODVAR, ONLY : IDIAGERROR
USE MOD_POLYGON_DRAW
USE MOD_POLYGON_UTL
USE MOD_POLYGON_PAR
USE MOD_SPOINTS_PAR
USE MOD_COLOURS

CONTAINS

 !###======================================================================
 LOGICAL FUNCTION STARTP1SAVELOAD(CODE,IQUIT)
 !###======================================================================
 IMPLICIT NONE
 INTEGER,INTENT(IN) :: CODE
 INTEGER,INTENT(OUT) :: IQUIT
 INTEGER :: IU,J,K,IOS
 CHARACTER(LEN=256) :: FNAME,LINE
 LOGICAL :: LEX

 STARTP1SAVELOAD=.FALSE.

 FNAME=TRIM(PREFVAL(1))//'\STARTPOINTS\*.isd'

 IF(CODE.EQ.0)THEN
  IF(SDFFNAME.EQ.'')THEN
   IF(.NOT.UTL_WSELECTFILE('iMOD Start Point Definition Files (*.isd)|*.isd|',  &
        LOADDIALOG+MUSTEXIST+PROMPTON+DIRCHANGE+APPENDEXT,FNAME,&
        'Load iMOD Start Point Definition File'))RETURN
  ELSE
   FNAME=SDFFNAME
  ENDIF

  !## test to see whether the file exists
  INQUIRE(FILE=FNAME,EXIST=LEX)
  IF(LEX)THEN
   IU=UTL_GETUNIT()
   CALL OSD_OPEN(IU,FILE=FNAME,STATUS='OLD',ACTION='READ,DENYWRITE',FORM='FORMATTED')
  ELSE
   IU=0
  ENDIF
  
  SHP%NPOL  =0
  SHPI   =0
  SHP%POL%IACT=0
  SHP%POL%PNAME=''

  IF(IU.GT.0)THEN
   DO
    READ(IU,*,IOSTAT=IOS)
    IF(IOS.NE.0)EXIT
    SHPI=SHPI+1
    IF(SHPI.GT.MAXSHAPES)THEN
     CALL WMESSAGEBOX(OKONLY,COMMONOK,EXCLAMATIONICON,'Total number of polygons read is '// &
              TRIM(ITOS(SHPI))//CHAR(13)//'* Maximal allowed is '//TRIM(ITOS(MAXSHAPES))//CHAR(13)//&
              'You can increase these settings in the menu-option: Preferences'//CHAR(13)//CHAR(13)// &
              'Selected file not read!','Error')
     CLOSE(IU)
     RETURN
    ENDIF
    READ(IU,*,IOSTAT=IOS) SHP%POL(SHPI)%PNAME
    IF(.NOT.STARTP1CHECK(IU,IOS,'ShapeName'//TRIM(ITOS(SHPI))))RETURN
    READ(IU,*,IOSTAT=IOS)
    IF(.NOT.STARTP1CHECK(IU,IOS,''))RETURN
    READ(IU,*,IOSTAT=IOS) SHP%POL(SHPI)%ITYPE
    IF(.NOT.STARTP1CHECK(IU,IOS,'ShapeType'//TRIM(ITOS(SHPI))))RETURN
    SELECT CASE (SHP%POL(SHPI)%ITYPE)
     CASE (ID_GRID)

     CASE (ID_POLYGON)
      READ(IU,*,IOSTAT=IOS) SPNT(SHPI)%IDX,SPNT(SHPI)%IDY
      SPNT(SHPI)%IDX=MAX(0.01D0,SPNT(SHPI)%IDX)
      SPNT(SHPI)%IDY=MAX(0.01D0,SPNT(SHPI)%IDY)
     CASE (ID_CIRCLE)
      !## overrule id_point -> id_circle
      SHP%POL(SHPI)%ITYPE=ID_CIRCLE
      READ(IU,*,IOSTAT=IOS) SPNT(SHPI)%IRADIUS,SPNT(SHPI)%ISX
      SPNT(SHPI)%IRADIUS=MAX(0.01D0,SPNT(SHPI)%IRADIUS)
      SPNT(SHPI)%ISX=MAX(0.01D0,SPNT(SHPI)%ISX)
     CASE (ID_POINT)
      !## overrule id_point -> id_circle
      SHP%POL(SHPI)%ITYPE=ID_POINT
      READ(IU,*,IOSTAT=IOS) SPNT(SHPI)%IRADIUS,SPNT(SHPI)%ISX
      SPNT(SHPI)%IRADIUS=MAX(0.01D0,SPNT(SHPI)%IRADIUS)
      SPNT(SHPI)%ISX=MAX(0.01D0,SPNT(SHPI)%ISX)
     CASE (ID_LINE)
      READ(IU,*,IOSTAT=IOS) SPNT(SHPI)%ISX
      SPNT(SHPI)%ISX=MAX(0.01D0,SPNT(SHPI)%ISX)
     CASE (ID_RECTANGLE)
      IF(.NOT.STARTP1CHECK(IU,IOS,'iMOD cannot recognize other shapes than polygon,circle and/or lines in an *,.isd-file'))RETURN
    END SELECT
    IF(.NOT.STARTP1CHECK(IU,IOS,'ShapeLat.Dimension'//TRIM(ITOS(SHPI))))RETURN
    READ(IU,*,IOSTAT=IOS) SPNT(SHPI)%TOPIDF
    IF(.NOT.STARTP1CHECK(IU,IOS,'ShapeTopIDF'//TRIM(ITOS(SHPI))))RETURN
    READ(IU,*,IOSTAT=IOS) SPNT(SHPI)%BOTIDF
    IF(.NOT.STARTP1CHECK(IU,IOS,'ShapeBotIDF'//TRIM(ITOS(SHPI))))RETURN
    READ(IU,'(A256)',IOSTAT=IOS) LINE
    READ(LINE,*,IOSTAT=IOS) SPNT(SHPI)%IREF
    IF(.NOT.STARTP1CHECK(IU,IOS,'ShapeIREF'//TRIM(ITOS(SHPI))))RETURN
    SPNT(SHPI)%IREF=MAX(0,MIN(1,SPNT(SHPI)%IREF))
    IF(SPNT(SHPI)%IREF.EQ.1)THEN
     READ(LINE,*,IOSTAT=IOS) SPNT(SHPI)%IREF,SPNT(SHPI)%REFIDF
     IF(.NOT.STARTP1CHECK(IU,IOS,'RefName'//TRIM(ITOS(SHPI))))RETURN
     SPNT(SHPI)%IREF=MAX(0,MIN(1,SPNT(SHPI)%IREF))
    ENDIF
    READ(IU,*,IOSTAT=IOS)  SPNT(SHPI)%ISZ
    IF(.NOT.STARTP1CHECK(IU,IOS,'ISZ'//TRIM(ITOS(SHPI))))RETURN
    READ(IU,*,IOSTAT=IOS)
    IF(.NOT.STARTP1CHECK(IU,IOS,''))RETURN
    READ(IU,*,IOSTAT=IOS) SHP%POL(SHPI)%N
    IF(.NOT.STARTP1CHECK(IU,IOS,'ShpNcrd'//TRIM(ITOS(SHPI))))RETURN
    IF(SHP%POL(SHPI)%N.GT.MAXSHPCRD)THEN
     CALL WMESSAGEBOX(OKONLY,COMMONOK,EXCLAMATIONICON,'The number of coordinates within one polygon is '// &
               TRIM(ITOS(SHP%POL(SHPI)%N))//CHAR(13)//'* Maximal allowed is '//TRIM(ITOS(MAXSHPCRD))//CHAR(13)//&
               'You can increase these settings in the menu-option: Preferences'//CHAR(13)//CHAR(13)// &
              'Selected file not read completely!','Error')
     CLOSE(IU)
     RETURN
    ENDIF
    ALLOCATE(SHP%POL(SHPI)%X(SHP%POL(SHPI)%N),SHP%POL(SHPI)%Y(SHP%POL(SHPI)%N))
    DO J=1,SHP%POL(SHPI)%N
     READ(IU,*,IOSTAT=IOS) SHP%POL(SHPI)%X(J),SHP%POL(SHPI)%Y(J)
     IF(.NOT.STARTP1CHECK(IU,IOS,'Polygon:'//TRIM(ITOS(SHPI)//',coord.:'//TRIM(ITOS(J)))))RETURN
    END DO
    READ(IU,*,IOSTAT=IOS)
    IF(.NOT.STARTP1CHECK(IU,IOS,''))RETURN
    SHP%POL(SHPI)%IACT =1
    SHP%POL(SHPI)%ICOLOR=ICOLOR(SHPI)
    SHP%POL(SHPI)%IWIDTH=2
   ENDDO
   CLOSE(IU)
  ENDIF
  
  SHP%NPOL=SHPI

  CALL WDIALOGSELECT(ID_DSPTAB1)
  CALL WDIALOGPUTMENU(IDF_MENU1,SHP%POL%PNAME,SHP%NPOL,SHP%POL%IACT)
  CALL POLYGON1FIELDS(ID_DSPTAB1)
  CALL STARTP1FIELDS_PUTTAB2()
  CALL STARTP1FIELDS()

 ELSEIF(CODE.EQ.1)THEN

  !## update last dialog fields --- error occured
  IF(.NOT.STARTP1FIELDS_GETTAB2(IQUIT,1))RETURN

  IF(SDFFNAME.EQ.'')THEN
   IF(.NOT.UTL_WSELECTFILE('iMOD Start Point Definition Files (*.isd)|*.isd|',  &
        SAVEDIALOG+PROMPTON+DIRCHANGE+APPENDEXT,FNAME,  &
        'Save iMOD Start Point Definition File'))RETURN
  ELSE
   INQUIRE(FILE=SDFFNAME,EXIST=LEX)
   IF(LEX)THEN
    CALL WMESSAGEBOX(YESNOCANCEL,COMMONNO,QUESTIONICON,'Do you want to overwrite the existing file:'// &
     CHAR(13)//TRIM(SDFFNAME),'Question')
    IF(WINFODIALOG(4).EQ.0)RETURN    !## cancel
    STARTP1SAVELOAD=.TRUE.
    IF(WINFODIALOG(4).EQ.2)RETURN    !## no
   ENDIF
   FNAME=SDFFNAME
  ENDIF
  
  IU=UTL_GETUNIT()
  CALL OSD_OPEN(IU,FILE=FNAME,STATUS='REPLACE',ACTION='WRITE,DENYREAD',FORM='FORMATTED')
  
  DO SHPI=1,SHP%NPOL
   IF(SHP%POL(SHPI)%N.GT.0)THEN
    WRITE(IU,'(50A1)') ('=',K=1,50)
    WRITE(IU,'(A)') TRIM(SHP%POL(SHPI)%PNAME)//' <-Shapename'
    WRITE(IU,'(50A1)') ('-',K=1,50)
    SELECT CASE (SHP%POL(SHPI)%ITYPE)
     CASE (ID_GRID)

     CASE (ID_POLYGON)
      LINE=TRIM(ITOS(SHP%POL(SHPI)%ITYPE))
      WRITE(IU,'(A)') TRIM(LINE)//',POLYGON'
      LINE=TRIM(RTOS(SPNT(SHPI)%IDX,'F',3))//','//TRIM(RTOS(SPNT(SHPI)%IDY,'F',3))
      WRITE(IU,'(A)') TRIM(LINE)
     CASE (ID_POINT)
      LINE=TRIM(ITOS(SHP%POL(SHPI)%ITYPE))
      WRITE(IU,'(A)') TRIM(LINE)//',POINT'
      LINE=TRIM(RTOS(SPNT(SHPI)%IRADIUS,'F',3))//','//TRIM(RTOS(SPNT(SHPI)%ISX,'F',3))
      WRITE(IU,'(A)') TRIM(LINE)
     CASE (ID_CIRCLE)
      LINE=TRIM(ITOS(SHP%POL(SHPI)%ITYPE))
      WRITE(IU,'(A)') TRIM(LINE)//',CIRCLE'
      LINE=TRIM(RTOS(SPNT(SHPI)%IRADIUS,'F',3))//','//TRIM(RTOS(SPNT(SHPI)%ISX,'F',3))
      WRITE(IU,'(A)') TRIM(LINE)
     CASE (ID_LINE)
      LINE=TRIM(ITOS(SHP%POL(SHPI)%ITYPE))
      WRITE(IU,'(A)') TRIM(LINE)//',LINE'
      LINE=TRIM(RTOS(SPNT(SHPI)%ISX,'F',3))
      WRITE(IU,'(A)') TRIM(LINE)
    END SELECT
    WRITE(IU,'(A)') '"'//TRIM(ADJUSTL(SPNT(SHPI)%TOPIDF))//'"'
    WRITE(IU,'(A)') '"'//TRIM(ADJUSTL(SPNT(SHPI)%BOTIDF))//'"'
    IF(SPNT(SHPI)%IREF.EQ.0)THEN
     LINE=TRIM(ITOS(SPNT(SHPI)%IREF))
     WRITE(IU,'(A)') TRIM(LINE)
    ELSE
     LINE=TRIM(ITOS(SPNT(SHPI)%IREF))//',"'//TRIM(ADJUSTL(SPNT(SHPI)%REFIDF))//'"'
     WRITE(IU,'(A)') TRIM(LINE)
    ENDIF
    LINE=TRIM(ITOS(SPNT(SHPI)%ISZ))
    WRITE(IU,'(A)') TRIM(LINE)
    WRITE(IU,'(50A1)') ('-',K=1,50)
    LINE=TRIM(ITOS(SHP%POL(SHPI)%N))
    WRITE(IU,'(A)') TRIM(LINE)//' <-No. Points Polygon'
    DO J=1,SHP%POL(SHPI)%N
     LINE=TRIM(RTOS(SHP%POL(SHPI)%X(J),'F',3))//','//TRIM(RTOS(SHP%POL(SHPI)%Y(J),'F',3))
     WRITE(IU,'(A)') TRIM(LINE)
    END DO
    WRITE(IU,'(50A1)') ('=',K=1,50)
   ENDIF
  END DO
  CLOSE(IU)
 ENDIF

 STARTP1SAVELOAD=.TRUE.

 END FUNCTION STARTP1SAVELOAD
 
 !###======================================================================
 LOGICAL FUNCTION STARTP1CHECK(IU,TIOS,LABEL)
 !###======================================================================
 IMPLICIT NONE
 INTEGER,INTENT(IN) :: IU,TIOS
 CHARACTER(LEN=*),INTENT(IN) :: LABEL

 STARTP1CHECK=.TRUE.
 IF(TIOS.NE.0)THEN
  CLOSE(IU)
  CALL WMESSAGEBOX(OKONLY,EXCLAMATIONICON,COMMONOK,'Selected file not read because there were errors inside the file'//CHAR(13)// &
   TRIM(LABEL),'Error')
  STARTP1CHECK=.FALSE.
 ENDIF

 END FUNCTION STARTP1CHECK
 
 !###======================================================================
 SUBROUTINE STARTP1FIELDS_PUTTAB2()
 !###======================================================================
 IMPLICIT NONE

 CALL WDIALOGSELECT(ID_DSPTAB1)
 CALL WDIALOGGETMENU(IDF_MENU1,SHP%POL%IACT)
 DO SHPI=1,SHP%NPOL; IF(SHP%POL(SHPI)%IACT.EQ.1)EXIT; END DO
 !## nothing selected --- return
 IF(SHPI.GT.SHP%NPOL)RETURN

 ISHAPE=SHPI

 CALL WDIALOGSELECT(ID_DSPTAB2)

 SELECT CASE (SHP%POL(ISHAPE)%ITYPE)
  CASE (ID_POLYGON)
   CALL WDIALOGPUTIMAGE(IDF_PICTURE1,ID_ICONPOLYGON,1)
   CALL WDIALOGFIELDSTATE(IDF_LABEL4,1)
   CALL WDIALOGFIELDSTATE(IDF_LABEL6,1)
   CALL WDIALOGPUTSTRING(IDF_LABEL4,'Distance X (m)')
   CALL WDIALOGPUTSTRING(IDF_LABEL6,'Distance Y (m)')
   CALL WDIALOGFIELDSTATE(IDF_REAL1,1)
   CALL WDIALOGFIELDSTATE(IDF_REAL3,1)
   CALL WDIALOGPUTDOUBLE(IDF_REAL1,SPNT(ISHAPE)%IDX)  !DISTX
   CALL WDIALOGPUTDOUBLE(IDF_REAL3,SPNT(ISHAPE)%IDY)  !DISTY
  CASE (ID_CIRCLE)
   CALL WDIALOGPUTIMAGE(IDF_PICTURE1,ID_ICONCIRCLE,1)
   CALL WDIALOGFIELDSTATE(IDF_LABEL4,1)
   CALL WDIALOGFIELDSTATE(IDF_LABEL6,1)
   CALL WDIALOGPUTSTRING(IDF_LABEL4,'Radius (m)')
   CALL WDIALOGPUTSTRING(IDF_LABEL6,'Sampling (m)')
   CALL WDIALOGFIELDSTATE(IDF_REAL1,1)
   CALL WDIALOGFIELDSTATE(IDF_REAL3,1)
   CALL WDIALOGPUTDOUBLE(IDF_REAL1,SPNT(ISHAPE)%IRADIUS)  !RADIUS
   CALL WDIALOGPUTDOUBLE(IDF_REAL3,SPNT(ISHAPE)%ISX)      !SAMPL.
  CASE (ID_LINE)
   CALL WDIALOGPUTIMAGE(IDF_PICTURE1,ID_ICONLINE,1)
   CALL WDIALOGFIELDSTATE(IDF_LABEL4,1)
   CALL WDIALOGFIELDSTATE(IDF_LABEL6,3)
   CALL WDIALOGPUTSTRING(IDF_LABEL4,'Sampling (m)')
   CALL WDIALOGFIELDSTATE(IDF_REAL1,1)
   CALL WDIALOGFIELDSTATE(IDF_REAL3,3)
   CALL WDIALOGPUTDOUBLE(IDF_REAL1,SPNT(ISHAPE)%ISX)  !SAMPL.
 END SELECT
 CALL WDIALOGPUTCHECKBOX(IDF_CHECK2,SPNT(ISHAPE)%ISNAP)

 CALL WDIALOGPUTSTRING(IDF_STRING2,SPNT(ISHAPE)%TOPIDF)
 CALL WDIALOGPUTSTRING(IDF_STRING3,SPNT(ISHAPE)%BOTIDF)
 CALL WDIALOGPUTCHECKBOX(IDF_CHECK1,SPNT(ISHAPE)%IREF)
 CALL WDIALOGPUTSTRING(IDF_STRING4,SPNT(ISHAPE)%REFIDF)
 CALL WDIALOGPUTINTEGER(IDF_INTEGER5,SPNT(ISHAPE)%ISZ)  !Z.SAMPL.

 END SUBROUTINE STARTP1FIELDS_PUTTAB2
 
 !###======================================================================
 LOGICAL FUNCTION STARTP1FIELDS_GETTAB2(IQUIT,ICHECK)
 !###======================================================================
 IMPLICIT NONE
 INTEGER,INTENT(OUT) :: IQUIT
 INTEGER,INTENT(IN) :: ICHECK
 
 STARTP1FIELDS_GETTAB2=.TRUE.
 IQUIT=0
 
! IF(ICHECK.EQ.0)RETURN

 STARTP1FIELDS_GETTAB2=.FALSE.

 !## something went wrong
 IF(ISHAPE.LE.0.OR.ISHAPE.GT.SHP%NPOL)THEN
  CALL WMESSAGEBOX(YESNO,QUESTIONICON,COMMONNO,'You did not specify any startpoints, do you realy want to quit?','Question')
  IF(WINFODIALOG(4).EQ.1)IQUIT=1
  RETURN
 ENDIF
 
 CALL WDIALOGSELECT(ID_DSPTAB2)
 CALL WDIALOGGETSTRING(IDF_STRING2,SPNT(ISHAPE)%TOPIDF)
 CALL WDIALOGGETSTRING(IDF_STRING3,SPNT(ISHAPE)%BOTIDF)
 CALL WDIALOGGETINTEGER(IDF_INTEGER5,SPNT(ISHAPE)%ISZ) 

 IF(LEN_TRIM(SPNT(ISHAPE)%TOPIDF).EQ.0)THEN
  IF(ICHECK.EQ.1)THEN; CALL WMESSAGEBOX(OKONLY,EXCLAMATIONICON,COMMONOK,'You need to specify TOP IDF.','Error'); RETURN; ENDIF
 ENDIF
 IF(LEN_TRIM(SPNT(ISHAPE)%BOTIDF).EQ.0)THEN
  IF(ICHECK.EQ.1)THEN; CALL WMESSAGEBOX(OKONLY,EXCLAMATIONICON,COMMONOK,'You need to specify BOT IDF.','Error'); RETURN; ENDIF
 ENDIF
 IF(SPNT(ISHAPE)%ISZ.LE.0)THEN
  IF(ICHECK.EQ.1)THEN; CALL WMESSAGEBOX(OKONLY,EXCLAMATIONICON,COMMONOK,'You need to specify Z-interval.','Error'); RETURN; ENDIF
 ENDIF

 SELECT CASE (SHP%POL(ISHAPE)%ITYPE)
  CASE (ID_POLYGON)
   CALL WDIALOGGETDOUBLE(IDF_REAL1,SPNT(ISHAPE)%IDX)  !DISTX
   CALL WDIALOGGETDOUBLE(IDF_REAL3,SPNT(ISHAPE)%IDY)  !DISTY
   IF(SPNT(ISHAPE)%IDX.LE.0.0D0.OR.SPNT(ISHAPE)%IDY.LE.0.0D0)THEN
    CALL WMESSAGEBOX(OKONLY,EXCLAMATIONICON,COMMONOK,'You need to specify positive values for'//CHAR(13)// &
      'Distance X and Distance Y.','Error')
    RETURN
   ENDIF
  CASE (ID_CIRCLE)
   CALL WDIALOGGETDOUBLE(IDF_REAL1,SPNT(ISHAPE)%IRADIUS)  !RADIUS
   CALL WDIALOGGETDOUBLE(IDF_REAL3,SPNT(ISHAPE)%ISX)      !SAMPL.
   IF(SPNT(ISHAPE)%IRADIUS.LE.0.0D0.OR.SPNT(ISHAPE)%ISX.LE.0.0D0)THEN
    CALL WMESSAGEBOX(OKONLY,EXCLAMATIONICON,COMMONOK,'You need to specify positive values for'//CHAR(13)// &
      'Radius and Sampling.','Error')
    RETURN
   ENDIF
  CASE (ID_LINE)
   CALL WDIALOGGETDOUBLE(IDF_REAL1,SPNT(ISHAPE)%ISX)  !SAMPL.
   IF(SPNT(ISHAPE)%ISX.LE.0.0D0)THEN
    CALL WMESSAGEBOX(OKONLY,EXCLAMATIONICON,COMMONOK,'You need to specify a positive value for Sampling.','Error')
    RETURN
   ENDIF
 END SELECT

 CALL WDIALOGGETCHECKBOX(IDF_CHECK1,SPNT(ISHAPE)%IREF)
 IF(SPNT(ISHAPE)%IREF.EQ.1)THEN
  CALL WDIALOGGETSTRING(IDF_STRING4,SPNT(ISHAPE)%REFIDF)
  IF(LEN_TRIM(SPNT(ISHAPE)%REFIDF).EQ.0)THEN
   IF(ICHECK.EQ.1)THEN; CALL WMESSAGEBOX(OKONLY,EXCLAMATIONICON,COMMONOK,'You need to enter an reference IDF.','Error'); RETURN; ENDIF
  ENDIF
 ENDIF

 CALL WDIALOGGETCHECKBOX(IDF_CHECK2,SPNT(ISHAPE)%ISNAP)
 
 STARTP1FIELDS_GETTAB2=.TRUE.
 
 END FUNCTION STARTP1FIELDS_GETTAB2
 
 !###======================================================================
 SUBROUTINE STARTP1FIELDS()
 !###======================================================================
 IMPLICIT NONE
 INTEGER :: I

 CALL WDIALOGSELECT(ID_DSPOINTS)
 I=0
 IF(SHP%NPOL.GT.0)THEN
  IF(SUM(SHP%POL(1:SHP%NPOL)%IACT).EQ.1)I=1
 ENDIF
 CALL WDIALOGTABSTATE(ID_DTAB,ID_DSPTAB2,I)
 
 CALL WDIALOGSELECT(ID_DSPTAB2)
 CALL WDIALOGGETCHECKBOX(IDF_CHECK1,I)
 CALL WDIALOGFIELDSTATE(IDF_STRING4,I)
 CALL WDIALOGFIELDSTATE(ID_OPEN3,I)

 END SUBROUTINE STARTP1FIELDS

 !###======================================================================
 SUBROUTINE STARTP1CLOSE(ICODE)
 !###======================================================================
 IMPLICIT NONE
 INTEGER,INTENT(IN) :: ICODE
 INTEGER :: IQUIT

 IDIAGERROR=1

 IF(ICODE.EQ.1)THEN
  !## save startpoint
  IF(.NOT.STARTP1SAVELOAD(1,IQUIT))THEN
   IF(IQUIT.EQ.0)RETURN
  ENDIF
 ENDIF

 IF(ALLOCATED(SPNT))DEALLOCATE(SPNT)

 CALL POLYGON1DRAWSHAPE(1,SHP%NPOL)
 CALL POLYGON1CLOSE()

 CALL WINDOWSELECT(0)
 CALL WMENUSETSTATE(ID_SPOINTS,2,0)

 CALL WDIALOGSELECT(ID_DSPOINTS)
 CALL WDIALOGUNLOAD()

 IDIAGERROR=0

 END SUBROUTINE STARTP1CLOSE
  
END MODULE MOD_SPOINTS_UTL